plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.28.0'
    implementation 'org.flywaydb:flyway-core:6.0.8'
    testImplementation 'junit:junit:4.12'
    testImplementation 'io.cucumber:cucumber-java:4.7.4'
    testImplementation 'io.cucumber:cucumber-junit:4.7.4'   
    testImplementation 'org.mockito:mockito-core:3.1.0'
}

application {
    mainClassName = 'ohtu.ts.App'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'gradle.cucumber', 'src/test/resources']
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

checkstyle {
    toolVersion '8.26';
}

test {
    environment 'OHTU_TS_TEST', 'true'
    testLogging.showStandardStreams = true
    systemProperties System.getProperties()
}

run {
    standardInput = System.in
}

jar {
    manifest {
        attributes 'Main-Class': 'ohtu.ts.App'
    }
}
